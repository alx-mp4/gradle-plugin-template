import java.util.regex.Matcher

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation files("libs/craftbukkit-1060.jar")
}

File ymlFile = file('src/main/resources/plugin.yml') as File

if (!ymlFile.exists()) {
    throw new GradleException("The 'plugin.yml' file does not exist in 'src/main/resources'.")
}

String ymlText = ymlFile.text
Matcher mainMatcher = (ymlText =~ /main:\s*(\S+)/)
Matcher versionMatcher = (ymlText =~ /version:\s*(\S+)/)
Matcher nameMatcher = (ymlText =~ /name:\s*(\S+)/)

if (!mainMatcher.find()) {
    throw new GradleException("The 'main' attribute wasn't found in the 'plugin.yml' file.")
}

if (versionMatcher.find()) {
    version = versionMatcher.group(1)
} else {
    throw new GradleException("The 'version' attribute wasn't found in the 'plugin.yml' file.")
}

if (!nameMatcher.find()) {
    throw new GradleException("The 'name' attribute wasn't found in the 'plugin.yml' file.")
}

tasks.build { dependsOn tasks.clean }

tasks.register('cleanBuild') {
    group = 'custom'
    dependsOn 'clean', 'build'
}

def pluginsPath = 'INSERT_ABSOLUTE_PATH_TO_SERVER_PLUGINS_FOLDER'

tasks.register("deletePluginJar", Delete) {
    group = 'custom'
    delete(files(pluginsPath + "${project.name}-${project.version}.jar")) // Or just ${project.name}.jar depending on how you've set it down below
}

tasks.register('replacePluginJar', Copy) {
    group = 'custom'
    from 'build/libs'
    into pluginsPath
    dependsOn 'clean', 'build', 'deletePluginJar'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': "${project.name}",
                'Implementation-Version': "${project.version}"
        )
    }

    archiveFileName = "${project.name}-${project.version}.jar"
    // archiveFileName = "${project.name}.jar" - If you wish to exclude the version from the .jar file name
}
