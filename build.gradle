import java.util.regex.Matcher

plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation files("libs/craftbukkit-1060.jar")
}

File ymlFile = file('src/main/resources/plugin.yml') as File
String ymlText = ymlFile.text
Matcher versionMatcher = (ymlText =~ /version:\s*(\S+)/)
if (versionMatcher.find()) version = versionMatcher.group(1)

tasks.build { dependsOn tasks.clean }

tasks.register('cleanBuild') {
    group = 'custom'
    dependsOn 'clean', 'build'
}

def pluginsPath = 'INSERT_ABSOLUTE_PATH_TO_SERVER_PLUGINS_FOLDER'

tasks.register("deletePluginJar", Delete) {
    group = 'custom'
    delete(files(pluginsPath + "${project.name}-${project.version}.jar"))
    // Or just ${project.name}.jar depending on how you've set it down below
}

tasks.register('replacePluginJar', Copy) {
    group = 'custom'
    from 'build/libs'
    into pluginsPath
    dependsOn 'clean', 'build', 'deletePluginJar'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': "${project.name}",
                'Implementation-Version': "${project.version}"
        )
    }

    archiveFileName = "${project.name}-${project.version}.jar"
    // archiveFileName = "${project.name}.jar" - If you wish to exclude the version from the .jar file name
}
