plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'org.example'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly files("libs/craftbukkit-1060.jar")
}

def yml = file('src/main/resources/plugin.yml').text
def m = (yml =~ /(?m)^\s*version:\s*["']?([^"'#\s]+)["']?/)
if (m.find()) version = m.group(1)

tasks.shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
}

tasks.build { dependsOn tasks.shadowJar }

tasks.register('cleanShadowJar') {
    group = 'shadow'
    dependsOn 'clean', 'shadowJar'
}

def pluginsPath = 'D:/Beta/CB1060/plugins/'

tasks.register("deletePluginJar", Delete) {
    group = 'shadow'
    delete(files(pluginsPath + "${project.name}-${project.version}.jar"))
}

tasks.register('replacePluginJar', Copy) {
    group = 'shadow'
    from 'build/libs'
    into pluginsPath
    dependsOn 'clean', 'shadowJar', 'deletePluginJar'
}